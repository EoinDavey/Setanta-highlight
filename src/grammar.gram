Program     := stmts=AsgnStmt* ws=_
AsgnStmt    := IfStmt
             | BlockStmt
             | NuairStmt
             | LeStmt
             | CCStmt
             | BrisStmt
             | CtlchStmt
             | GniomhStmt
             | ToradhStmt
             | AssgnStmt
             | DefnStmt
             | Expr
NonAsgnStmt := IfStmt
             | NuairStmt
             | LeStmt
             | CCStmt
             | BrisStmt
             | ToradhStmt
             | BlockStmt
             | AssgnStmt
             | Expr
IfStmt      := ws=_ 'm[áa]' &gap expr=Expr &gap stmt=NonAsgnStmt elsebranch={ws=_ 'n[oó]' &gap stmt=NonAsgnStmt}?
BlockStmt   := wsa=_ '{' blk=AsgnStmt* wsb=_ '}'
NuairStmt   := ws=_ 'nuair-a' expr=Expr &gap stmt=NonAsgnStmt
LeStmt      := wsa=_ 'le' &gap id=ID wsb=_ 'idir' wsc=_ '\('strt=Expr wsd=_ ',' end=Expr step={ws=_ ',' step=Expr}? wse=_ '\)' stmt=NonAsgnStmt
DefnStmt    := wsa=_ id=ID wsb=_ ':=' wsc=_ expr=Expr
AssgnStmt   := wsa=_ lhs=Postfix wsb=_ op=AsgnOp wsc=_ expr=Expr
GniomhStmt  := wsa=_ 'gn[íi]omh' &gap id=ID wsb=_ '\(' args=CSIDs? wsc=_ '\)' wsd=_ '{'
    stmts=AsgnStmt*
wse=_ '}'
CtlchStmt   := wsa=_ 'creatlach' &gap id=ID tuis={wsa=_ 'ó' wsb=_ id=ID}? wsb=_ '{'
    gniomhs=GniomhStmt*
wsc=_ '}'
BrisStmt    := ws=_ 'bris'
CCStmt      := ws=_ 'chun-cinn'
ToradhStmt  := ws=_ 'toradh' &gap exp=Expr?
Expr        := And
And         := head=Or tail={ws=_ '\&' trm=Or}*
Or          := head=Eq tail={ws=_ '\|' trm=Eq}*
Eq          := head=Comp tail={ws=_ op='[!=]=' trm=Comp}*
Comp        := head=Sum tail={ws=_ op=Compare trm=Sum}*
Sum         := head=Product tail={ws=_ op=PlusMinus trm=Product}*
Product     := head=Prefix tail={ws=_ op=MulDiv trm=Prefix}*
Prefix      := ws=_ op='-|!'? pf=Postfix
Postfix     := at=ObjLookups ops=PostOp*
ObjLookups  := attrs={id=ID '@' !wspace}* root=Atom
PostOp      := '\(' args=CSArgs? ws=_ '\)' | '\[' expr=Expr ws=_ '\]'
Atom        :=  ws=_ '\(' trm=Expr '\)'
             | ID
             | Litreacha
             | Int
             | Bool
             | Neamhni
             | ListLit
ListLit     := wsa=_ '\[' els=CSArgs? wsb=_ '\]'
CSArgs      := head=Expr tail={ws=_ ',' exp=Expr}*
CSIDs       := head=ID tail={ws=_ ',' id=ID}*
ID          := ws=_ !{Keyword gap} id='[a-zA-Z_áéíóúÁÉÍÓÚ]+'
Bool        := ws=_ bool='f[ií]or|br[eé]ag'
Neamhni     := ws=_ 'neamhn[ií]'
Int         := ws=_ int='-?[0-9]+(?:\.[0-9]+)?'
Litreacha   := ws=_ '\'' val='([^\'\\]|\\.)*' '\''
_           := sp=wspace*
wspace      := '(?:\s|>--(?:(?!--<).)*(--<|\n|$))'
gap         := { wspace | '[^a-zA-Z0-9áéíóúÁÉÍÓÚ]' }+ | '$'
PlusMinus   := '\+|-'
AsgnOp      := '=|\+=|\*=|-=|%=|\/='
MulDiv      := '\*|\/\/|%|\/'
Compare     := '<=|>=|<|>'
Keyword     := 'm[áa]' | 'n[oó]' | 'nuair-a' | 'f[ií]or|br[eé]ag'
    | 'gn[ií]omh' | 'chun-cinn' | 'neamhn[ií]' | 'toradh' | 'creatlach'
